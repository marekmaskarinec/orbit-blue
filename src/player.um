
import (
	"anim.um"
	"atlas.um"
	"ent.um"
	"input.um"
	"rect.um"
	"th.um"

	"bullet.um"
	"glob.um"
	"res.um"
	"stage.um"
)

const TAG = "player.um"

const (
	rotSpeed = 0.15
	maxSpeed = 0.15
	minSpeed = 0.01
	accelRate = 0.04
)

type Player* = struct {
	e: ^ent.Ent
	st: ^stage.Stage

	anim: anim.Anim
	
	speed: real32
}

fn (this: ^Player) mk*(st: ^stage.Stage) {
	this.e = new(ent.Ent, ent.mk({}))
	res.setEnt(this.e, "Moon_Ship_Idle")
	this.e.t.o = this.e.r.getDims().mul(this.e.t.s).divf(2)
	this.speed = minSpeed
	this.st = st
}

fn (this: ^Player) getShootPos(): th.Vf2 {
	return th.Vf2{ 32, 16 }.transformed(this.e.t)
}

fn (this: ^Player) update*() {
	if input.isPressed(input.key_left) {
		this.e.t.r -= rotSpeed * th.delta
	}

	if input.isPressed(input.key_right) {
		this.e.t.r += rotSpeed * th.delta
	}

	if input.isPressed(input.key_down) {
		this.speed -= this.speed * accelRate
		if this.speed < minSpeed {
			this.speed = minSpeed
		}
	} else {
		this.speed += this.speed * accelRate
		if this.speed > maxSpeed {
			this.speed = maxSpeed
		}
	}

	if input.isJustPressed(input.key_down) {
		this.anim = res.setEnt(this.e, "Moon_Ship_Idle")
	}

	if input.isJustReleased(input.key_down) {
		this.anim = res.setEnt(this.e, "Moon_Ship_Moving")
	}

	if input.isJustPressedc(' ') {
		b := new(bullet.Bullet)
		b.mk(this.st)
		b.e.t.r = this.e.t.r
		b.e.t.p = this.getShootPos().sub(b.e.r.getDims().divf(2))
		this.st.spawn(b)
	}

	this.e.t.p = this.e.t.p.add(
		th.Vf2{ this.speed * th.delta, 0 }.rotated({}, this.e.t.r)
	)
}

fn (this: ^Player) draw*() {
	this.anim.animate(th.time)
	this.e.draw()
}

fn (this: ^Player) getEnt*(): ^ent.Ent {
	return this.e
}

