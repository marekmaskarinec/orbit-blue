
import (
	"atlas.um"
	"canvas.um"
	"ent.um"
	"rect.um"
	"th.um"
	"tilemap.um"
	"window.um"

	"coulau/logs.um"

	"res.um"
	"util.um"
)

const TAG = "stage.um"

type (
	Model* = interface {
		mk(st: ^Stage)
		update()
		draw()
		getEnt(): ^ent.Ent
	}

	Stage* = struct {
		walls: tilemap.Tilemap
		brwalls: tilemap.Tilemap
		models: []Model

		modelUpdateIdx: int
	}
)

fn init*() {
}

fn (this: ^Stage) mk*() {
	this.walls = tilemap.mk(
		util.genRandomTilemap(80, 80, 1),
		80,
		res.atlases["Moon_Stone"]
	)
	this.walls.scale = res.images["Moon_Stone"].scale.x
	this.walls.collMask = { true }
	this.walls.pos = { -200, -200 }

	this.brwalls = tilemap.mk(
		util.genRandomTilemap(80, 80, 4),
		80,
		res.atlases["Breakable_Block"]
	)
	this.brwalls.scale = res.images["Breakable_Block_COMPILE"].scale.x
	this.brwalls.collMask = { true, true, true, true }
	this.brwalls.pos = { -200, -200 }

	this.modelUpdateIdx = -1
}

fn (this: ^Stage) getWallColl*(e: ^ent.Ent, ic, pos: ^th.Vf2): bool {
	if this.walls.getColl(e^, ic, pos) {
		return true
	}

	return this.brwalls.getColl(e^, ic, pos)
}

fn (this: ^Stage) deleteCurrentModel*() {
	if this.modelUpdateIdx < 0 {
		error("deleteCurrentModel called out of the model update loop")
	}

	idx := this.modelUpdateIdx
	this.modelUpdateIdx++

	if len(this.models) == 1 {
		this.models = {}
		return
	}

	this.models[idx] = this.models[len(this.models) -1]
	this.models = slice(this.models, 0, len(this.models) - 1)
}

fn (this: ^Stage) spawn*(m: Model) {
	this.models = append(this.models, m)
}

fn (this: ^Stage) update*() {
	this.modelUpdateIdx = 0
	for _:=0; this.modelUpdateIdx < len(this.models); this.modelUpdateIdx++ {
		this.models[this.modelUpdateIdx].update()
	}
	this.modelUpdateIdx = -1
}

fn (this: ^Stage) draw*() {
	for i in this.models {
		this.models[i].draw()
	}

	this.walls.draw()
	this.brwalls.draw()
}

